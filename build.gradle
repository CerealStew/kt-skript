plugins {
    id "org.jetbrains.kotlin.jvm" version "1.2.50"
    id "org.jetbrains.kotlin.kapt" version "1.2.50"
    id "com.github.johnrengelman.shadow" version "2.0.4"
}

group "de.randombyte"
version "1.0"

repositories {
    jcenter()
    maven { url "https://repo.spongepowered.org/maven/" }
    maven { url "http://repo.bstats.org/content/repositories/releases" }
    maven { url "https://jitpack.io" }
}

configurations {
    compile.extendsFrom shadow
    compile.extendsFrom kapt
}

dependencies {
    shadow("org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.2.50")
    shadow("org.jetbrains.kotlin:kotlin-compiler-embeddable:1.2.50")
    shadow("org.jetbrains.kotlin:kotlin-script-util:1.2.50") { transitive = false}
    shadow("org.jetbrains.kotlin:kotlin-reflect:1.2.50")
    shadow("io.github.lukehutch:fast-classpath-scanner:2.9.5")
    kapt "org.spongepowered:spongeapi:7.0.0"
    compile "com.github.randombyte-developer:PlaceholderAPI:v4.5.1"
    compile "com.github.randombyte-developer:byte-items:v2.2.6"
    shadow "org.bstats:bstats-sponge:1.2"
}

jar.enabled = false

shadowJar {
    configurations = [project.configurations.shadow]

    //relocate "kotlin", "de.randombyte.ktskript.shaded.kotlin"
    relocate "io.github.lukehutch.fastclasspathscanner", "de.randombyte.ktskript.shaded.fcs"
    relocate "org.bstats", "de.randombyte.ktskript.shaded.bstats"

    def packages = [
            "com.google.common.base", "com.google.common.collect", "com.google.common.math", "com.google.common.primitives", "com.google.common.util",
            "com.google.gwt",
            "com.intellij",
            "com.sun",
            "gnu.trove",
            "javaslang",
            "javax.inject",
            "net.jpountz",
            "one.util",
            "org.apache", "org.fusesource", "org.intellij", "org.iq80", "org.jdom", /*"org.jetbrains",*/ "org.jline", "org.picocontainer",
            "net.rubygrapefruit"
    ]

    packages.forEach {
        relocate it, "de.randombyte.ktskript.shaded.$it"
    }

    classifier = null // Remove "-all" suffix from output file name
}
build.dependsOn shadowJar

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}
