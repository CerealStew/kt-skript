plugins {
    id "org.jetbrains.kotlin.jvm" version "1.2.50"
    id "org.jetbrains.kotlin.kapt" version "1.2.50"
    id "com.github.johnrengelman.shadow" version "2.0.4"
}

group "de.randombyte"
version "1.2"

repositories {
    jcenter()
    maven { url "https://repo.spongepowered.org/maven/" }
    maven { url "http://repo.bstats.org/content/repositories/releases" }
    maven { url "https://jitpack.io" }
}

configurations {
    compile.extendsFrom shadow
    compile.extendsFrom kapt
}

def kotlinVersion = "1.2.50"

dependencies {
    shadow("org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlinVersion")
    shadow("org.jetbrains.kotlin:kotlin-compiler-embeddable:$kotlinVersion")
    shadow("org.jetbrains.kotlin:kotlin-script-util:$kotlinVersion") { transitive = false}
    shadow("org.jetbrains.kotlin:kotlin-reflect:$kotlinVersion")
    shadow("io.github.lukehutch:fast-classpath-scanner:2.9.5")
    kapt "org.spongepowered:spongeapi:7.0.0"
    compile "com.github.randombyte-developer:PlaceholderAPI:v4.5.1"
    compile "com.github.randombyte-developer:byte-items:v2.2.6"
    shadow "org.bstats:bstats-sponge:1.2"
}

jar.enabled = false

shadowJar {
    configurations = [project.configurations.shadow]

    def packages = [
            "io.github.lukehutch.fastclasspathscanner",
            "javaslang",
            "net.jpountz", "net.rubygrapefruit",
            "one.util",
            "org.bstats", "org.intellij", "org.iq80",
            "org.jetbrains.ide", "org.jetbrains.org.objectweb.asm"
    ]

    packages.forEach {
        relocate it, "de.randombyte.ktskript.shaded.$it"
    }

    classifier = null // Remove "-all" suffix from output file name
}
build.dependsOn shadowJar

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}
